autoload -Uz colors
colors

# 区切り文字の設定
autoload -Uz select-word-style
select-word-style default
zstyle ':zle:*' word-chars "_-./;@"
zstyle ':zle:*' word-style unspecified

# 複数ファイルの mv
# 例: zmv *.txt *.txt.bk
autoload -Uz zmv
alias zmv='noglob zmv -W'

# history
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

# PROMPT with vcs_info
autoload -Uz vcs_info
setopt prompt_subst
zstyle ':vcs_info:git:*' check-for-changes true
zstyle ':vcs_info:git:*' stagedstr "%F{yellow}+"
zstyle ':vcs_info:git:*' unstagedstr "%F{red}*"
zstyle ':vcs_info:*' formats "%F{magenta}%c%u%m(%b)%f"
zstyle ':vcs_info:*' actionformats "%F{red}[%b|%a]"

## Hooks in vcs_info
zstyle ':vcs_info:git+set-message:*' hooks \
  git-untracked \
  git-stash-count

## show '?' if exists untracked files
function +vi-git-untracked() {
  if [ "$(git status --porcelain | awk '{print $1}' | grep -F '??' 2>/dev/null)" != "" ]; then
    hook_com[unstaged]+='?'
  fi
}

## show the size of stash list
function +vi-git-stash-count() {
  local stash
  stash=$(git stash list 2>/dev/null | wc -l | tr -d ' ')
  if [ "${stash}" -gt 0 ]; then
      hook_com[misc]+="\$${stash}"
  fi
}

precmd () { vcs_info }
PROMPT='%{${fg[green]}%}%n@%m %{${fg[cyan]}%}%~ ${vcs_info_msg_0_} %{${fg[blue]}%}%# %{${reset_color}%}'

########################################
# bindkey
#
# vi-like keybind
bindkey -v

# Ctrl+r で history のインクリメンタルサーチ, Ctrl+s で逆順
bindkey '^r' history-incremental-pattern-search-backward
bindkey '^s' history-incremental-pattern-search-forward

# コマンドを途中まで入力後, Ctrl+p で history から絞り込み, Ctrl+b で逆順
autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^p" history-beginning-search-backward-end
bindkey "^b" history-beginning-search-forward-end

########################################
# completion
#
autoload -Uz compinit
compinit

# 補完で大文字にもマッチ
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# sudo の後ろでコマンド名を補完する
zstyle ':completion:*:sudo:*' command-path /usr/local/sbin /usr/local/bin \
                   /usr/sbin /usr/bin /sbin /bin

########################################
# setopt
#
# 同時に起動した zsh の間でヒストリを共有する
setopt share_history

# 同じコマンドをヒストリに残さない
setopt hist_ignore_all_dups

# スペースから始まるコマンド行はヒストリに残さない
setopt hist_ignore_space

# ヒストリに保存するときに余分なスペースを削除する
setopt hist_reduce_blanks

# ディレクトリ名だけで cd する
setopt auto_cd

# 高機能なワイルドカード展開を使用する
setopt extended_glob

# コマンドミスを修正
setopt correct
